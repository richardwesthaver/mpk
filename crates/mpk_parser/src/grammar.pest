program = _{ SOI ~ "\n"* ~ (stmt ~ ("\n"+ | SEP+))* ~ stmt? ~ EOI }

stmt = _{ expr }

expr = {
    assgmt
  | verbExpr
  | fnExpr
  | fnCall
  | sysExpr
  | nouns
}

monadic = { ((verb ~ adverb) | verb) ~ expr }

dyadic = { ( monadic | nouns )  ~ ((verb ~ adverb) | verb) ~ expr }

verbExpr = _{ monadic | dyadic }

sysExpr = { sysverb ~ expr? }

fnExpr = { "{" ~ (LB ~ name ~ SEP? ~ (name ~ SEP?)* ~ RB)? ~ expr_or_noun ~ "}"}

fnCall = { name ~ "[" ~ ((expr_or_noun ~ SEP) | expr_or_noun ~ SEP?)* ~ "]" }

assgmt = { name ~ ASSIGN ~ expr_or_noun }

noun = _{ decimal | int | name | string | symbol | dict | table | list}

nouns = { noun+ }

expr_or_noun = _{ sysExpr | fnExpr | verbExpr | nouns }

list = { ("(" ~ (expr_or_noun ~ SEP?)* ~ ")") }

dict = { "[" ~ (name ~ (ASSIGN ~ expr_or_noun)?)? ~ (SEP ~ name ~ ASSIGN ~ expr_or_noun)* ~ SEP?  ~ "]" }

table = { "[" ~ "[" ~ ((name ~ (ASSIGN ~ nouns)?) ~ (SEP ~ name ~ ASSIGN ~ nouns)* ~ SEP?)* ~ "]" ~ ( (name ~ (ASSIGN ~ nouns)?) ~ (SEP ~ name ~ ASSIGN ~ nouns)* ~ SEP?)* ~ "]" }

verb = {
  "+" | "-" | "*" | "%" | "!" | "&"
| "|" | "<" | ">" | "=" | "~" | ","
| "^" | "#" | "_" | "$" | "?" | "@"
| "."
}

adverb = {
  "':" | "/:" | "\\:" | "'" | "/" | "\\"
}

sysverb = {
  "\\" ~ ("\\" | name) ~ list*
}

int = @{ "-"? ~ ASCII_DIGIT+ }

decimal = @{ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }

name = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

string = @{ "\"" ~ ( "\"\"" | (!"\"" ~ ANY) )* ~ "\"" }

symbol = @{ "`" ~ name }

ASSIGN = { ":" }

SEP = _{ ";" }
LB = {"["}
RB = {"]"}
WHITESPACE = _{ " " | "\t" }

COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }