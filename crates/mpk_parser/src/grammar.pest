program = _{ SOI ~ "\n"* ~ (stmt ~ "\n"+) * ~ stmt? ~ EOI }

stmt = _{ expr }

expr = {
    assgmtExpr
  | monadicExpr
  | dyadicExpr
  | nouns
  | sysExpr
}

monadicExpr = { ((verb ~ adverb) | verb) ~ expr }

dyadicExpr = { ( monadicExpr | nouns )  ~ ((verb ~ adverb) | verb) ~ expr }

sysExpr = { sysverb ~ expr }

assgmtExpr = { name ~ ":" ~ expr }

noun = _{ decimal | int | name | string | symbol | "(" ~ expr ~ (SEP ~ expr)* ~ SEP? ~ ")" }

nouns = { noun+ }

verb = {
  "+" | "-" | "*" | "%" | "!" | "&"
| "|" | "<" | ">" | "=" | "~" | ","
| "^" | "#" | "_" | "$" | "?" | "@"
| "."
}

adverb = {
  "':" | "/:" | "\\:" | "'" | "/" | "\\"
}

sysverb = {
  "\\sesh" | "\\http" | "\\osc" | "\\db"
}

int = @{ "-"? ~ ASCII_DIGIT+ }

decimal = @{ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }

name = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

string = @{ "\"" ~ ( "\"\"" | (!"\"" ~ ANY) )* ~ "\"" }

symbol = @{ "`" ~ name }

SEP = _{ ";" }

WHITESPACE = _{ " " | "\t" }

COMMENT = _{ "/ " ~ (!"\n" ~ ANY)* }